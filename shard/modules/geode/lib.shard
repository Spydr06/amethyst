let
    stdenv = import ./stdenv.shard;

    typecheckConfig = arg: (case arg of
            _ :: null => {};
            _ :: set => arg;
            other => builtins.throw 
                "Package configuration may only accept `set' types for configuration, got ${builtins.typeOf arg} instead."
        );

    baseConfig = typecheckConfig << (builtins.defaultFunctionArg "config");

    defaultArgs = pkgFunc: pkgs: let
            config = baseConfig pkgFunc;
        in
            { inherit lib stdenv pkgs config; };

    wrapPackage = pkgFunc: baseArgs: let
            result = pkgFunc baseArgs;
            override = overrideConfig :: set | function:
                let
                    base = baseConfig pkgFunc;
                    extra = (case overrideConfig of
                        _ :: function => overrideConfig base;
                        _ => base // overrideConfig
                    );
                    newArgs = baseArgs // { config = extra; };
                in wrapPackage ({ config?extra, lib, stdenv, pkgs, ... }: pkgFunc { inherit config lib stdenv pkgs; }) newArgs;
        in builtins.setAttr "override" override result;

    callPackageWith = dependencyPkgs: pkgs: pkgFunc :: function:
        let
            pkg = wrapPackage pkgFunc $ defaultArgs pkgFunc dependencyPkgs;
        in
            builtins.setAttr pkg.publicName pkg pkgs;
    
    callPackage = pkgFunc :: function: pkgFunc << (defaultArgs pkgFunc);

    ensureInstalled = let
            checkInstalled = { outPath, ... }: builtins.isPath outPath;
        in builtins.all checkInstalled;

    ensureDependencies = { name, version, ... }: depList :: list: if ensureInstalled depList
        then 0
        else builtins.seq
            (geode.debug.dump depList)
            (builtins.throw "Could not satisfy dependencies for `${name}-${version}'");

    createEnv = env :: set: value:
        let
            saveEnv = stdenv.applyEnvSet env;
            restoreEnv = stdenv.applyEnvSet saveEnv;
        in builtins.seqList [
            saveEnv
            value
            restoreEnv
            value
        ];

    sequencedTasks = tasks :: list: deriv :: set:
        let
            checkExitCode = { name, task }:
                let 
                    isOk = x: x == null || x == 0;
                    failWith = message: builtins.throw "Task `${name}` of ${deriv.name}-${deriv.version} failed with ${message}.";
                in (case task of
                    x if isOk x       => false;
                    exitCode :: int   => failWith "exit code ${exitCode}";
                    message :: string => failWith ": ${message}";
                    result            => failWith "unknown value ${result}"
                );
        in
        assert (builtins.filter checkExitCode tasks) == []; 0;

    /* builder:
       - fetch
       - patch
       - configure
       - build
       - check
       - install
       - postInstall
    */
    derivationBuilder = deriv :: set:
        let
            phases = [ "fetch" "patch" "configure" "build" "check" "install" "post install" ];

            buildDeps = deriv.buildDependencies or [];
            runtimeDeps = deriv.buildRuntimeDependencies or [];
             
            installBuildDeps = ensureDependencies deriv buildDeps;
            installRuntimeDeps = ensureDependencies deriv runtimeDeps;

            filterPhase = name: builtins.hasAttr (name + "Phase") deriv;

            builderPhase = name: let
                    phase = builtins.getAttr (name + "Phase") deriv;
                    infoMessage = geode.debug.println ">>> Entering phase ${name} of ${deriv.name}-${deriv.version}";
                in builtins.seq
                    infoMessage
                    { inherit name; task = createEnv deriv.env or {} $ phase; };

            builderTasks = 
                [ { name = "build dependencies"; task = installBuildDeps; } ]
                ++ (stdenv.lazyMap builderPhase $ stdenv.lazyFilter filterPhase phases)
                ++ [ { name = "build dependencies"; task = installRuntimeDeps; } ];

        in sequencedTasks builderTasks deriv; 

    mkDerivation = attrs :: set: 
        let
            throwMissingAttributeError = name: attrName: builtins.throw "Derivation `${name}' has missing attribute `${attrName}'";

            name = attrs.name or $ throwMissingAttributeError "<unknown>" "name";
            version = attrs.version or $ throwMissingAttributeError name "version";
        in
        {
            publicName = attrs.publicName or name;
            publicVersion = attrs.publicVersion or version;

            exports = createEnv attrs.env or {} $ attrs.exports or {};

            outPath = geode.derivation {
                inherit name;
                inherit version;

                builder = derivationBuilder attrs;
            };
            __toString = self: builtins.toString self.outPath;
        };

    mkPackage = mkDerivation;

    mkCollection = attrs :: set:
        let
            deriv = mkDerivation attrs;
            providePkgs = attrs.providePkgs or {};
        in deriv // { pkgs = providePkgs; };

    mkKernelModule = mkDerivation << (import ./kernelModule.shard);

    mkShell = decl :: set: null;

    mkSystemConfig = mkDerivation << (import ./systemConfig.shard);

    mkdirRec = path: if stdenv.fileExists path
            then 0
            else builtins.seq
                (mkdirRec (geode.file.dirname path))
                (geode.file.mkdir path);

    shell = {
        cp = args: 
            geode.proc.spawn "cp" (builtins.map builtins.toString args) true;

        install = args:
            geode.proc.spawn "install" (builtins.map builtins.toString args) true;
    };

    installFile = { src, dst, mode ? "744" }: let dstDir = geode.file.dirname (geode.prefix + dst); 
            in assert (mkdirRec dstDir) == 0;
                shell.install ["-m" mode src (geode.prefix + dst)];

    installDir = src: dst: let 
            dstDir = geode.file.dirname (geode.prefix + dst);
        in assert (mkdirRec dstDir) == 0;
            shell.cp ["-rp" src (geode.prefix + dst)];

    fetchGitRepoTo = to: args:
        let
            fetchRepo = url: if geode.file.exists $ builtins.toPath "${to}/.git"
                then geode.git.pullRepo to
                else geode.git.cloneRepo url to;
            checkoutBranch = geode.git.checkoutBranch;
        in (case args of
            url :: string => fetchRepo url;
            { url, branch } => builtins.seq
                (fetchRepo url)
                (checkoutBranch branch to);
            _ => builtins.throw "invalid arguments provided to `lib.fetchGitRepoTo'"
        );
 
    defaultSourceDir = builtins.toPath "./source";

    fetchFileTo = dest: if stdenv.fileExists dest
        then _: dest
        else geode.net.downloadFile dest;
        
    fetchTarballTo = dest: geode.archive.extractTar << fetchFileTo dest;

    seqList = xs :: list: if builtins.all (x: x == 0) xs then 0 else 1;

    lib = {
        defaultArgs = defaultArgs (_: null);
        inherit defaultSourceDir;

        prefix = geode.prefix;

        inherit createEnv;
        inherit callPackage;
        inherit callPackageWith;

        fix = f: let x = f x; in x;

        inherit ensureDependencies;

        inherit mkDerivation;
        inherit mkPackage;
        inherit mkShell;
        inherit mkSystemConfig;
        inherit mkCollection;
        inherit mkKernelModule;

        optionalPkg = builtins.when;
        inherit sequencedTasks;

        inherit mkdirRec;

        inherit installDir;
        inherit installFile;

        inherit fetchGitRepoTo;
        fetchGitRepo = fetchGitRepoTo defaultSourceDir;

        inherit fetchFileTo;
        fetchFile = url :: string: fetchFileTo (builtins.basename $ builtins.toPath url) url;

        inherit fetchTarballTo;
        fetchTarball = url :: string: fetchTarballTo (builtins.basename $ builtins.toPath url) url;

        realpath = p :: path | string:
            let
                result = geode.proc.spawnPipe "realpath" [ (builtins.toString p) ] "";
            in if result.exitCode == 0
                then builtins.toPath $ builtins.trim $ result.stdout
                else builtins.throw "realpath exited with code ${result.exitCode}: ${result.stderr}";

        make = args :: list: 
            geode.proc.spawn "make" (builtins.map builtins.toString (args ++ ["-j" (geode.proc.nJobs)])) true;

        spawnProcess = args :: list:
            geode.proc.spawn (builtins.head args) (builtins.map builtins.toString $ builtins.tail args) true;

        requireFile = file: if stdenv.fileExists file
            then null
            else "`${file}': No such file or directory";

        installFiles = files :: list: lib.seqList $ builtins.map installFile files;

        writeFile = file: data: builtins.seq
            (mkdirRec $ geode.file.dirname file)
            (geode.file.writeFile file $ builtins.toString data);

        runPrecompiled = args :: list:
            if (geode.getenv "NIXOS_HOST") == null
                then args
                else [ "steam-run" ] ++ args;

        storeEntry = geode.storeEntry;

        inherit seqList;
        seq = a: b: if a == 0 && b == 0 then 0 else 1;
    };
in lib
